/* eslint-disable */

path / {
  read() { auth.token.email === 'admin@aplano.de' }
  write() { auth.token.email === 'admin@aplano.de' }
}

path /allUsers {
  read() { auth.uid !== null }
  write() { auth.uid !== null }
}

path /dbVersion {
  read() { true }
  write() { false }
}

path /emailInvites {
  read() { auth.uid !== null }
  write() { auth.uid !== null }
}

path /accounts/{accID} {
  read() { isOwnAccount(accID, auth.uid) }
  write() { isOwnAccount(accID, auth.uid) }
}

path /accountsFlat/{accID} { // accountsFlat is just for maintenance purposes
  read() { false }
  write() { isOwnAccount(accID, auth.uid) }
}

path /allUsers/{uid} is TransUser;

path /accounts/{accID}/uses/{uID} is User;
path /accounts/{accID}/positions/{pID} is Position;
path /accounts/{accID}/branches/{pID} is Branch;

path /accounts/{accID}/absencePlaner/absences/{absenceID} is Absence;
path /accounts/{accID}/absencePlaner/weeklyAbsences/{weekID}/{absenceID} is WeeklyAbsence;

path /accounts/{accID}/roster/corrections/{corID} is Correction;
path /accounts/{accID}/roster/extraHours/{weekID}/{eID} is ExtraHours;
path /accounts/{accID}/roster/shiftWeeks/{weekID}/{shiftID} is Shift;
path /accounts/{accID}/roster/templatesFlat/{tempID} is TemplateFlat;
path /accounts/{accID}/roster/weekSums/{sumID} is Number;


// Types //

type TransUser {
  account: String,
  userID: String,
  email: String,
  timestamp: Number,
  deleted: Number|Null,
}

type WeeklyAbsence {
  avgMins: Number,
  firstDay: Number,
  id: String,
  lastDay: Number,
  type: String,
  useMins: Boolean|Null,
  user: String,
  workDays: Object,
}

type Absence {
  avgMins: Number,
  effectiveDays: Number,
  endDate: Number,
  id: String,
  startDate: Number,
  status: String,
  touchingWeeks: Object,
  type: String,
  user: String,
  modified: Object|Null,
  workDays: Object|Null,
  year: Number,
  unpaid: Boolean|Null,
  note: String|Null,
  yearUser: String|Null
}

type Location {
  id: String,
  name: String,
  color: String,
  deleted: Boolean|Null
}

type Branch {
  id: String,
  name: String,
  locations: Location[]|Null
}

type Position {
  id: String,
  name: String,
  color: String,
  nr: Number,
  shortcut: String,
}

type ExtraHours{
  branch: String,
  branchDay: String|Null,
  day: Day,
  id: String,
  mins: Number,
  user: String,
  note: String|Null,
}

type Correction{
  initial: Boolean|Null,
  mins: Number,
  user: String,
  week: String,
}

type Day extends String {
  validate() {this=='mo'||this =='tu'||this =='we'||this=='th'||this=='fr'||this=='sa'||this=='su'}
}

type Shift {
  id: String,
  s: Number,
  e: Number,
  b: Number,
  user: String,
  day: Day,
  edit: Object | Null,
  note: String | Null,
  branch: String,
  branchDay: String,
  location: String | Null,
  position: String | Null,
}

type TemplateFlat {
  branch: String,
  id: String,
  name: String
}

type User {
  id: String,
  name: String,
  branches: Object,
  email: String|Null,
  position: String,
  weeklyMins: Object,
  status: String,
  isAdmin: Number|Null,
  isSuperAdmin: Number|Null,
  workDays: Object,
  deleted: Number|Null
}

// Functions //

isOwnAccount(accID, uid) { root.allUsers[uid]['account'] === accID }

// indexing for Query-Optimistion //

path /accounts/{accID}/absencePlaner/absences {
  index() { ['year', 'yearUser'] }
}

path /accounts/{accID}/absencePlaner/absencesWeekly/{weekID} {
  index() { ['user'] }
}

path /accounts/{accID}/absencePlaner/absenceCorrections {
  index() { ['year'] }
}

path /accounts/{accID}/roster/extraHours/{weekID} {
  index() { ['user', 'branch'] }
}

path /accounts/{accID}/roster/dayNotes/{weekID} {
  index() { ['branch'] }
}

path /accounts/{accID}/roster/shiftWeeks/{weekID} {
  index() { ['user', 'branch', 'branchDay'] }
}

path /allUsers/{uid} {
  index() { ['userID'] }
}

path /accounts/{accID}/roster/shiftList {
  index() { ['user'] }
}

path /accounts/{accID}/roster/extraHoursList {
  index() { ['user'] }
}
